// add guava 27 to the classpath of the *build script*
// see https://github.com/spotbugs/spotbugs-gradle-plugin/issues/120
// solution taken from https://github.com/GoogleContainerTools/jib/issues/591
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("com.google.guava:guava:27.0.1-jre")
    }
}

plugins {
    id 'maven-publish'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'pl.allegro.tech.build.axion-release' version '1.10.1'
    id "com.github.spotbugs" version "2.0.0"
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        maven { // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
        // maven central has packages that are missing from the google mirror
        mavenCentral()
        mavenLocal()
    }
    scmVersion {
        ignoreUncommittedChanges = false
        tag {
            prefix = 'release'
        }
    }
    version = scmVersion.version
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: "maven-publish"
    apply plugin: "java"
    apply plugin: 'com.github.johnrengelman.shadow'
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    group 'de.saar.minecraft.infrastructure'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    
    javadoc {
	    failOnError = false
    }
    
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/minecraft-saar/infrastructure")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                    password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
                }
            }
        }
        publications {
            "$project.name"(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }

        }
    }
    
    spotbugs {
        toolVersion = '4.0.0-beta4'
        ignoreFailures = false
    }

    checkstyle {
        toolVersion = "8.28"
    }
    checkstyleMain {
        source ='src/main/java'
    }
    checkstyleTest {
        source ='src/test/java'
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            text.enabled = true
            html.enabled = false
        }
    }
    
    dependencies {
        implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.16.0'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.16.0'
        implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.16.0'
        compileOnly "javax.annotation:javax.annotation-api:1.2"
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
        testImplementation "org.mockito:mockito-core:2.25.1"
    }
}

// Local Variables:
// groovy-indent-offset: 4
// indent-tabs-mode: nil
// End:
