plugins {
	id 'java-library'
	id 'com.google.protobuf' version '0.8.16'
}

def grpcVersion = '1.43.1'
def protocVersion = '3.11.0'

// As of Oct 2021, there is no gRPC binary for Mac Arm.
// We therefore enforce the use of the Intel binary.
def archSuffix = osdetector.os == "osx" ? ":osx-x86_64" : ""

dependencies {
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"
    
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    
    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}


protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}${archSuffix}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}${archSuffix}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

idea {
    module {
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc");
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java");
    }
}

// Local Variables:
// groovy-indent-offset: 4
// indent-tabs-mode: nil
// End:
